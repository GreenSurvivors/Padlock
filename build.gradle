plugins {
    id 'java-library'
    id 'maven-publish'
    id("io.papermc.paperweight.userdev") version "1.7.1"
    id("xyz.jpenilla.run-paper") version "2.3.0"
}
// Suppiled by Jenkins
ext.majorVersion = 2
ext.minorVersion = 6
ext.minecraftVersion = "1.20.4"

ext.buildNumber = System.env.BUILD_NUMBER == null ? "dev" : "build" + "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null               : "$System.env.JAVADOCS_DIR"

// differet version convension from Nyaa plugins
group = "de.greensurvivors"
archivesBaseName = "Padlock"
version = "$majorVersion.$minorVersion.$buildNumber".toString()
/*
// comment this in, if you want to test with a specific Mc version instead of the one defined in minecraftVersion
runServer {
    minecraftVersion("1.20.4")
}
 */

// extra compile warnings
compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { name 'Paper'; url 'https://repo.papermc.io/repository/maven-public/' }
    maven { name 'sk89q-repo'; url "https://maven.enginehub.org/repo/" } //worldguard
    maven { url 'https://jitpack.io' } // vault
}

dependencies {
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation("com.github.ben-manes.caffeine:caffeine:3.1.8") // caches
    implementation("de.mkammerer:argon2-jvm:2.11") // native password hashing with argon2

    paperweight.paperDevBundle("$minecraftVersion-R0.1-SNAPSHOT")
    compileOnly ('com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT') { transitive = false }
    compileOnly('com.github.MilkBowl:VaultAPI:1.7') { transitive = false }
}

processResources {
    expand version: project.version
}

// maven publications
tasks.register('sourcesJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.java.srcDirs
}

tasks {
    // Configure reobfJar to run when invoking the build task
    assemble {
        dependsOn(reobfJar)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifactId "padlock"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "$mavenDirectory"
        }
    }
}
