plugins {
    id 'java-library'
    id 'maven-publish'
    id("io.papermc.paperweight.userdev") version "1.5.9"
    id("xyz.jpenilla.run-paper") version "1.0.6"
}
// Suppiled by Jenkins
ext.majorVersion = 3
ext.minorVersion = 0
ext.minecraftVersion = "1.20.1"

ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "build" + "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null               : "$System.env.JAVADOCS_DIR"

sourceCompatibility = 17
targetCompatibility = 17

// differet version convension from Nyaa plugins
group = "de.greensurvivors"
archivesBaseName = "GreenLocker"
version = "$majorVersion.$minorVersion.$buildNumber".toString()
runServer {
    minecraftVersion("1.20.1")
}

// extra compile warnings
compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { name 'Paper'; url 'https://repo.papermc.io/repository/maven-public/' }
    maven { name 'sk89q-repo'; url "https://maven.enginehub.org/repo/" } //worldguard
    maven { name 'playpro'; url 'https://maven.playpro.com/' } //coreProtect
    maven {name 'dmulloy2';   url 'https://repo.dmulloy2.net/repository/public/'}
}

dependencies {
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.jetbrains:annotations:23.0.0'
    paperweight.paperDevBundle("1.20.2-R0.1-SNAPSHOT")
    compileOnly ('com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT') { transitive = false }
    compileOnly ('net.coreprotect:coreprotect:21.0'){ transitive = false}
}

processResources {
    expand version: project.version
}

// maven publications
tasks.register('sourcesJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.java.srcDirs
}

tasks {
    // Configure reobfJar to run when invoking the build task
    assemble {
        dependsOn(reobfJar)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifactId "greenlocker"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "$mavenDirectory"
        }
    }
}
